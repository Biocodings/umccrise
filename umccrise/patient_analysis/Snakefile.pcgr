"""
Somatic calls
-------------
Run ensemble and raw calls through PCGR and combine with the cnv calls.
"""
from umccrise import panel_of_normals


# PCGR struggles with anything but the basic chromosome setup.
# It also ignores any variant not marked `PASS` so might as well remove others to save on transfer times.
rule pcgr_somatic_vcf:
    input:
        rules.pon_pass.output[0]
    output:
        vcf = 'umccrised/{batch}/somatic/pcgr/ensemble-pon-pass-pcgr.vcf.gz',
        tbi = 'umccrised/{batch}/somatic/pcgr/ensemble-pon-pass-pcgr.vcf.gz.tbi'
    shell:
        'pcgr_prep {input} |'
        ' bcftools view -f.,PASS -t 1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,X,Y -Oz -o {output.vcf}'
        ' && tabix -p vcf {output.vcf}'


# PCGR also wants a slightly different format for the CNS data:
rule pcgr_cns:
    input:
        lambda wc: join(batch_by_name[wc.batch].tumor.dirpath, f'{batch_by_name[wc.batch].name}-cnvkit.cns')
    output:
        'umccrised/{batch}/somatic/pcgr/cnvkit-pcgr.tsv'
    shell:
        'echo -e "Chromosome\\tStart\\tEnd\\tSegment_Mean" > {output} && cat {input} | grep -v ^chromosome | cut -f 1,2,3,5 >> {output}'


# Prepare it for submission to PCGR
rule pcgr_germline:
    input:
        vcf = rules.prep_germline.output[0]
    output:
        vcf = 'umccrised/{batch}/germline/pcgr/ensemble-cancer.vcf.gz',
        tbi = 'umccrised/{batch}/germline/pcgr/ensemble-cancer.vcf.gz.tbi'
    shell:
        'pcgr_prep {input.vcf} |'
        ' bcftools view -f.,PASS -t 1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,X,Y -Oz -o {output.vcf}'
        ' && tabix -p vcf {output.vcf}'
# Can review the `.txt` file in Excel or submit the VCF to frameworks such as [gene.iobio](http://gene.iobio.io/).


rule upload_to_pcgr:
    input:
        rules.pcgr_somatic_vcf.output,
        rules.pcgr_cns.output,
        rules.pcgr_germline.output
    params:
        url = f'ubuntu@{pcgr_url}.ap-southeast-2.compute.amazonaws.com',
        som_dir = 'umccrised/{batch}/somatic/pcgr',
        germ_dir = 'umccrised/{batch}/germline/pcgr',
        batch = lambda wc: wc.batch
    output:
        'umccrised/.snakemake/pcgr_{batch}.done'
    shell:
        'scp -r {params.som_dir} {params.url}:/mnt/pcgr-0.4.2.1/{params.batch}_somatic'
        ' && '
        'scp -r {params.germ_dir} {params.url}:/mnt/pcgr-0.4.2.1/{params.batch}_germline'
        ' && '
        'touch {output}'
        # 'conda install -c conda-forge ansible>=2.3'
        # 'ansible-playbook aws.yaml'
        # 'ssh ubuntu@<AWS INSTANCE>'
        # 'cd /mnt/work/pcgr-*'
        # './pcgr.py --input_vcf examples/tumor_sample.COAD.vcf.gz --input_cna examples/tumor_sample.COAD.cna.tsv /mnt/work/pcgr-* output tumor_sample.COAD'


rule pcgr:
    input:
        expand(rules.pcgr_somatic_vcf.output, batch=batch_by_name.keys()),
        expand(rules.pcgr_cns.output, batch=batch_by_name.keys()),
        expand(rules.pcgr_germline.output, batch=batch_by_name.keys())
    output:
        'umccrised/pcgr.done'
    shell:
        'touch {output}'
