'''
Somatic calls
-------------
Run ensemble and raw calls through PCGR and combine with the cnv calls.
'''

# PCGR struggles with anything but the basic chromosome setup. It also ignores any variant not marked `PASS` so might as well remove others to save on transfer times:
rule pcgr_vcf:
    input:
        vcf = join(run.date_dir, '{batch}-{caller}-annotated.vcf.gz')
    output:
        vcf = 'umccrised/{batch}/somatic/pcgr/{caller}.vcf.gz',
        tbi = 'umccrised/{batch}/somatic/pcgr/{caller}.vcf.gz.tbi'
    shell:
        'bcftools view -f .,PASS -r 1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,X,Y {input.vcf} -Oz > {output.vcf}'
        ' && tabix -p vcf {output.vcf}'


# PCGR also wants a slightly different format for the CNS data:
rule pcgr_cns:
    input:
        lambda wc: join(batch_by_name[wc.batch].tumor.dirpath, f'{wc.batch}-cnvkit.cns')
    output:
        'umccrised/{batch}/somatic/pcgr/cnvkit-pcgr.tsv'
    shell:
        'echo -e "Chromosome\\tStart\\tEnd\\tSegment_Mean" > {output} && cat {input} | grep -v ^chromosome | cut -f 1,2,3,5 >> {output}'


rule upload_to_pcgr:
    input:
        vcf = 'umccrised/{batch}/somatic/pcgr/ensemble.vcf.gz',
        vcf_tbi = 'umccrised/{batch}/somatic/pcgr/ensemble.vcf.gz.tbi',
        cns = rules.pcgr_cns.output[0]
    params:
        url = 'ubuntu@ec2-13-54-23-149.ap-southeast-2.compute.amazonaws.com'
    output:
        'umccrised/.snakemake/pcgr_{batch}.done'
    shell:
        'touch {output}'
        # 'conda install -c conda-forge ansible>=2.3'
        # 'ansible-playbook aws.yaml'
        # 'ssh ubuntu@<AWS INSTANCE>'
        # 'cd /mnt/work/pcgr-*'
        # './pcgr.py --input_vcf examples/tumor_sample.COAD.vcf.gz --input_cna examples/tumor_sample.COAD.cna.tsv /mnt/work/pcgr-* output tumor_sample.COAD'


# Ensemble calls to be submitted to CGI. Sharing functionality does not seem to work at this point. Ensemble calls only include variants that `PASS` so no additional filtering required.
# Finally, for the local analysis with MutationalPatterns generate UCSC-versions of the ensemble calls:
rule cgi:
    input:
        'umccrised/{batch}/somatic/pcgr/ensemble.vcf.gz'
    output:
        'umccrised/{batch}/somatic/rstudio/ensemble-ucsf.vcf'
    shell:
        'gunzip -c {input}'
        " | py -x \"x.replace('##contig=<ID=', '##contig=<ID=chr') if x.startswith('#') else 'chr' + x\""
        ' | grep -v chrG > {output}'
# All done; file needs to be submitted to CGI and PCGR manually for now. 


## Allelic frequencies
# AF is not yet integrated into PCGR or CGI. We can extract those from VarDict or FreeBayes for plotting purposes, but still need to look up AF for genes of interest manually. Not entirely ideal but for a rough summary plot but this is going to be sufficient. Can revisit once we've unified AF information across callers:

rule af_freqs:
    input:
        vcf = join(run.date_dir, '{batch}-vardict-annotated.vcf.gz')
    params:
        tumor_name = lambda wc: batch_by_name[wc.batch].tumor.name
    output:
        'umccrised/{batch}/somatic/af/af_vardict_tumor.txt'
    shell:
        'bcftools view -f .,PASS {input.vcf} -s {params.tumor_name} -Ou'
        ' | bcftools query -f "[%AF]\\n" > {output}'

rule af_freqs_az300:
    input:
        vcf = join(run.date_dir, '{batch}-vardict-annotated.vcf.gz'),
        az300 = az300
    params:
        tumor_name = lambda wc: batch_by_name[wc.batch].tumor.name
    output:
        'umccrised/{batch}/somatic/af/af_vardict_tumor_az300.txt'
    shell:
       'bcftools view -f .,PASS {input.vcf} -s {params.tumor_name} -Ov'
        ' | bedtools intersect -a stdin -b {input.az300} -header'
        ' | bcftools query -f "%CHROM\\t%POS\\t%ID\\t%REF\\t%ALT\\t[%AF]\\t%INFO/ANN\n" > {output}'

# Generating the plot manually at a later stage.


rule somatic:
    input:
        expand(rules.af_freqs_az300.output, batch=batch_by_name.values()),
        expand(rules.af_freqs.output, batch=batch_by_name.values()),
        expand(rules.cgi.output, batch=batch_by_name.values()),
        expand(rules.pcgr_vcf.output, batch=batch_by_name.values(), caller=list(batch_by_name.values())[0].tumor.variantcallers),
        expand(rules.upload_to_pcgr.output, batch=batch_by_name.values())
    output:
        'umccrised/.snakemake/somatic.done'
    shell:
        'touch {output}'



